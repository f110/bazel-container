load("@bazel_gazelle//:def.bzl", "DEFAULT_LANGUAGES", "gazelle", "gazelle_binary")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("//private/deb:deb_pkg.bzl", "deb_pkg")
load("@rules_distroless//distroless:defs.bzl", "cacerts", "passwd")
load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_mklink")

gazelle(name = "gazelle")

pkg_tar(
    name = "bazelisk_bin",
    files = {
        "@bazelisk//file": "/usr/local/bin/bazel",
    },
    mode = "0755",
)

cacerts(
    name = "cacerts",
    package = "@debian12_ca-certificates//:data",
)

passwd(
    name = "passwd",
    entries = [
        {
            "gecos": ["root"],
            "gid": 0,
            "shell": "/sbin/nologin",
            "home": "/root",
            "uid": 0,
            "password": "x",
            "username": "root",
        },
    ],
)

pkg_mklink(
    name = "which_link",
    link_name = "usr/bin/which",
    target = "/usr/bin/which.debianutils",
)

pkg_mklink(
    name = "aclocal_link",
    link_name = "usr/bin/aclocal",
    target = "/usr/bin/aclocal-1.16",
)

pkg_mklink(
    name = "automake_link",
    link_name = "usr/bin/automake",
    target = "/usr/bin/automake-1.16",
)

pkg_mklink(
    name = "awk_link",
    link_name = "usr/bin/awk",
    target = "/usr/bin/mawk",
)

pkg_mklink(
    name = "nawk_link",
    link_name = "usr/bin/nawk",
    target = "/usr/bin/mawk",
)

pkg_mklink(
    name = "pager_link",
    link_name = "usr/bin/pager",
    target = "/bin/more",
)

pkg_filegroup(
    name = "links-debian",
    srcs = [
        ":aclocal_link",
        ":automake_link",
        ":awk_link",
        ":nawk_link",
        ":pager_link",
        ":which_link",
    ],
)

pkg_tar(
    name = "alternatives-link",
    srcs = [":links-debian"],
)

pkg_tar(
    name = "base-debian",
    mode = "0755",
    deps = deb_pkg(
        "debian12",
        "base-files",
        "base-passwd",
        "tzdata",
        "netbase",
        "libc6",
        "libssl3",
        "coreutils",
        "debianutils",
        "grep",
        "sed",
        "findutils",
        "mawk",
        "g++",
        "zip",
        "unzip",
        "tar",
        "gzip",
        "patch",
        "linux-libc-dev",
        "curl",
        "git",
        "python3-minimal",
        "python3-distutils",
        "libpython3.11-stdlib",
        "bash",
        "dash",
        "automake",
        "pkg-config",
    ),
)

oci_image(
    name = "bazelisk",
    architecture = "amd64",
    entrypoint = ["/usr/local/bin/bazel"],
    labels = {
        "org.opencontainers.image.source": "https://github.com/f110/bazel-container",
    },
    os = "linux",
    tars = [
        ":base-debian",
        ":alternatives-link",
        ":passwd",
        ":cacerts",
        ":bazelisk_bin",
    ],
)

oci_tarball(
    name = "bazelisk.tar",
    image = ":bazelisk",
    repo_tags = [
        "bazel-container:bazelisk",
        "bazel-container:bazelisk-debian",
    ],
)

oci_push(
    name = "push",
    image = ":bazelisk",
    remote_tags = [
        "bazelisk-debian12",
        "bazelisk-v1.25.0-debian12",
    ],
    repository = "ghcr.io/f110/bazel-container",
)
